@echo off
@REM DOSBox compatible newproject batch file
@REM  BHZ Skeleton Module for MBBS v6.x
@REM  Mark Seelye (bhz) 2023
@REM  burninghorizon.com, bhz.co
IF "%1"=="" goto usage
IF "%2"=="" goto usage
set _bbssrc=C:\BBSV6\SRC
IF "%3"=="" goto start
set _bbssrc=%3
:start
set _devid=%1
set _modid=%2
set _dest=%_bbssrc%\%_devid%\%_modid%
set _from=BHZSKLN
set _c=""

:devdir
IF EXIST %_bbssrc%\%_devid% goto moddir
mkdir %_bbssrc%\%_devid%

:moddir
IF EXIST %_bbssrc%\%_devid%\%_modid% goto files
mkdir %_bbssrc%\%_devid%\%_modid%
@REM No loops in DOSBox batch :(

:files
set _name=%_devid%%_modid%

:file1
set _ext=BCR
set _file=%_dest%\%_name%.%_ext%
IF EXIST %_file% goto file2
COPY %_from%.%_ext% %_file% > nul
set _c=%_file%

:file2
set _ext=LNK
set _file=%_dest%\%_name%.%_ext%
IF EXIST %_file% goto file3
set _c=%_c%,%_file%
@echo \run286\bc3\lib\c0phdll  + > %_file%
@echo \bbsv6\phobj\%_name% >> %_file%
@echo \bbsv6\%_name% >> %_file%
@echo nul >> %_file%
@echo phapi galimp msgimp gsblimp /Tdd /s /n >> %_file%
@echo \bbsv6\dlib\nodef >> %_file%

:file3
set _ext=MDF
set _file=%_dest%\%_name%.%_ext%
IF EXIST %_file% goto file4
@rem COPY %_from%.%_ext% %_file% > nul
set _c=%_c%,%_file%
@echo ; %_name% Module MDF > %_file%
@echo. >> %_file%
@echo Module Name: %_name% Module >> %_file%
@echo. >> %_file%
@echo Developer: Your Name 20??-??-?? >> %_file%
@echo. >> %_file%
@echo Requires: >> %_file%
@echo Replaces: >> %_file%
@echo. >> %_file%
@echo Install: >> %_file%
@echo. >> %_file%
@echo Online user manual: >> %_file% 
@echo. >> %_file%
@echo DLLs: %_name% >> %_file%
@echo MSGs: %_name% >> %_file%
@echo. >> %_file%
@echo Btrieve page size:    128 >> %_file%
@echo Btrieve files:        1 >> %_file%
@echo. >> %_file%
@echo Cleanup: >> %_file%
@echo. >> %_file%
@echo Event-1: >> %_file%
@echo Event-2: >> %_file%
@echo Event-3: >> %_file%
@echo Event-4: >> %_file%
@echo. >> %_file%
@echo Add-On Utility: >> %_file%

:file4
set _ext=MSG
set _file=%_dest%\%_name%.%_ext%
IF EXIST %_file% goto file5
COPY %_from%.%_ext% %_file% > nul
set _c=%_c%,%_file%

:file5
set _file=%_dest%\MAKEMOD.BAT
IF EXIST %_file% goto file6
COPY MAKEMOD.BAT %_file% > nul
set _c=%_c%,%_file%

:file6
set _file=%_dest%\MODMAIN.C
IF EXIST %_file% goto file7
COPY MODMAIN.C %_file% > nul
set _c=%_c%,%_file%

:file7
set _file=%_dest%\MODMAIN.H
IF EXIST %_file% goto file8
COPY MODMAIN.H %_file% > nul
set _c=%_c%,%_file%

:file8
set _file=%_dest%\MODDEFS.H
IF EXIST %_file% goto makefile1
set _c=%_c%,%_file% > %_file%
@echo // Autogenerated by NEWMOD.BAT >> %_file%
@echo #ifndef __%_name%_MODMAIN_H__ >> %_file%
@echo #define __%_name%_MODMAIN_H__ >> %_file%
@echo // >> %_file%
@echo #include "MODMAIN.H" >> %_file%
@echo #include "%_name%.H" >> %_file%
@echo // >> %_file%
@echo #define MODVER "v1.0.0" >> %_file%
@echo #define MODNAM "%_name%" >> %_file%
@echo // >> %_file%
@echo void init(void); >> %_file%
@echo void EXPORT init__%_name%(void) { >> %_file%
@echo   init(); >> %_file%
@echo } >> %_file%
@echo #endif >> %_file%

:makefile1
set _src=EXTNDABL.MAK
set _file=%_dest%\%_src%
IF EXIST %_file% goto makefile2
COPY %_src% %_file% > nul
set _c=%_c%,%_file%

:makefile2
set _src=MAKEFILE.MAK
set _file=%_dest%\%_src%
IF EXIST %_file% goto makefile3
COPY %_src% %_file% > nul
set _c=%_c%,%_file%

:makefile3
set _file=%_dest%\MAKEFILE.inc
IF EXIST %_file% goto makefile4
set _c=%_c%,%_file%
@echo DEVID=%_devid% > %_file%
@echo MODID=%_modid% >> %_file%

:makefile4
set _file=%_dest%\MAKEMOD.BAT
IF EXIST %_file% goto nextsteps
COPY MAKEMOD.BAT %_file% > nul
set _c=%_c%,%_file%

:nextsteps
IF %_c%=="" goto nothing

:done
echo Done!
goto end

:nothing
echo.
echo Nothing to do!
echo.
dir /w %_dest%
echo.
goto end

:usage
echo.
echo Create new project batch (DOSBox compatible) v0.1
echo.
echo  BHZ Skeleton Module for MBBS v6.x
echo  Mark Seelye (bhz) 2023
echo  burninghorizon.com, bhz.co
echo.
echo USAGE:
echo     NEWMOD.BAT DID MODID [BBSv6 dir]
echo       DID is your desired dev id.  ex: ABC (3 characters)
echo       MODID is your desired module id.  ex: BLURP (1-5 characters)
echo       [BBSv6 dir] is optional, defaults to %_bbssrc%
echo       Note: for best effect enter DID and MODID in ALL CAPS
echo.

goto end

:end
cd %_dest%
